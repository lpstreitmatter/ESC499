{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Microgrid Model\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "county = \"SanJoaquin\""
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 1. Load Packages"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#import Pkg; Pkg.add(\"NBInclude\"); #If there are any new packages to add later"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Gurobi\n",
    "using Plots; #plotly();\n",
    "using VegaLite  \n",
    "using DataFrames, CSV, PrettyTables\n",
    "using FileIO\n",
    "using Dates\n",
    "using TimeZones\n",
    "using NBInclude"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 2. Specify Directories"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "#inputs_dir = \"/Users/laurenstreitmatter/Documents/Fourth_Year/ESC499/Inputs\";\n",
    "inputs_dir = \"/home/lstreitmatter/Projects/thesis_runs/Inputs\";\n",
    "runs_df = CSV.read(joinpath(inputs_dir,\"runs.csv\"), DataFrame);\n",
    "results_dir = \"/home/lstreitmatter/Projects/thesis_runs/Results\";\n",
    "#results_dir = \"/Users/laurenstreitmatter/Documents/Fourth_Year/ESC499/Results\";"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Helper Functions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function calculate_cap_annual(C, ror, n)\n",
    "    return (C*(ror/(1-(1+ror)^(-n))))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function create_df(DecisionVar,idcol::String,value_name::String)\n",
    "    #DecisionVar = 2D decision variable indexed over an id and time\n",
    "    #idcol = string with the id column name e.g., \"h_id\"\n",
    "    #value_name = string with the name of the decision variable value e.g., \"H2SOC\"\n",
    "\n",
    "    df = DataFrame(value.(DecisionVar).data, :auto)\n",
    "    df_ax1 = value.(DecisionVar).axes[1]\n",
    "    df_ax2 = value.(DecisionVar).axes[2]\n",
    "    df_cols = names(df)\n",
    "    insertcols!(df, 1, :idcol => df_ax1)\n",
    "    df = stack(df, Not(:idcol), variable_name = :time)\n",
    "    df.time = foldl(replace, [df_cols[i] => df_ax2[i] for i in 1:length(df_ax2)], init=df.time)\n",
    "    rename!(df, :idcol => idcol)\n",
    "    rename!(df, :value => value_name)\n",
    "    df.time = convert.(DateTime,df.time)\n",
    "\n",
    "    return df\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function create_df_one_axis(DecisionVar,value_name::String)\n",
    "    df = DataFrame(value = value.(DecisionVar).data)  \n",
    "    df_ax1 = value.(DecisionVar).axes[1]\n",
    "    insertcols!(df, 1, :time => df_ax1) \n",
    "    df.time = convert.(DateTime,df.time)\n",
    "    rename!(df, :value => value_name)\n",
    "    \n",
    "    return df\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Solver Function\n",
    "Function to solve economic dispatch problem (over one year, one zone/node, deterministic)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function renewable_microgrid(config, gen_df, solarcf_df, load_df, hydrogen_df, lmp_df, mer_df, battery_df)\n",
    "    RM = Model(Gurobi.Optimizer)\n",
    "    \n",
    "    #Define Parameters from Config file \n",
    "    ror = parse(Float32,config[config[!,:Parameter] .== \"ror\", :Value][1])\n",
    "    carbon_cap = parse(Float32,config[config[!,:Parameter] .== \"carbon_cap\", :Value][1])\n",
    "    carbon_cost = parse(Float32,config[config[!,:Parameter] .== \"carbon_cost\", :Value][1])/1000\n",
    "    voll = parse(Float32,config[config[!,:Parameter] .== \"voll\", :Value][1])\n",
    "    county_print = config[config[!,:Parameter] .== \"county\", :Value][1]\n",
    "    rps = parse(Float32,config[config[!,:Parameter] .== \"rps\", :Value][1])\n",
    "\n",
    "    #Define sets \n",
    "        #A set of all renewable generators\n",
    "    G_renew = gen_df[gen_df[!,:renewable_gen] .== 1,:type]\n",
    "        #A set of non-renewable generators\n",
    "    G_fossil = gen_df[gen_df[!,:renewable_gen] .== 0,:type]\n",
    "        #A set of all generators\n",
    "    G = gen_df.type\n",
    "\n",
    "        #A set of the timepoints (most granular time input)\n",
    "    TP = load_df.time\n",
    "        #Variable for the amount of time between timepoints, in minutes\n",
    "    tp_spacing = convert(Dates.Minute,TP[2]-TP[1])\n",
    "        #Integer version of tp_spacing converted to hours\n",
    "    tp_spacing_int = convert(Float32,tp_spacing.value)/60\n",
    "\n",
    "    #Define outage timepoints when Pgrid must be zero\n",
    "    outage_length = parse(Float32,config[config[!,:Parameter] .== \"outage_duration\", :Value][1])\n",
    "    outage_start = config[config[!,:Parameter] .== \"outage_start\", :Value][1]\n",
    "    temp_start = DateTime(outage_start,dateformat\"yyyy-mm-dd HH:MM:SS+zz\") + Dates.Year(1) #offset to 2021 to avoid leap year\n",
    "    temp_end = temp_start + Dates.Day(outage_length) - tp_spacing\n",
    "    tp_outage = temp_start:tp_spacing:temp_end;\n",
    "    tp_outage = tp_outage .- Dates.Year(1); #reset back to 2020\n",
    "    println(\"here2\")\n",
    "\n",
    "    println(\"here3\")\n",
    "    # Capacity building/sizing decision variables\n",
    "    @variables(RM, begin\n",
    "        0 <=    vGenCapkW[G]        \n",
    "        0 <=    vBatteryCapkW       <= battery_df.max_storage_kw[1]\n",
    "        0 <=    vElectroCapkW       <= hydrogen_df.electrolyzer_max_capacity_kw[1]\n",
    "        0 <=    vFCCapkW            <= hydrogen_df.fuel_cell_max_capacity_kw[1]\n",
    "        0 <=    vTankCapkg          <= hydrogen_df.tank_max_capacity_kg[1]\n",
    "    end)\n",
    "\n",
    "    # Operational decision variables   \n",
    "    @variables(RM, begin\n",
    "        0 <=    vGEN[G, TP]         # power generation/production (kW)\n",
    "\n",
    "        0 <=    vELECTROLYZER[TP]   # electricity assigned to hydrogen production (kW)\n",
    "        0 <=    vH2SOC[TP]          # hydrogen state of charge (kg H2)\n",
    "        0 <=    vFUELCELL[TP]       # fuel cell electricity production (kW)\n",
    "\n",
    "        0 <=    vCharge[TP]         # amount to charge battery (kW)\n",
    "        0 <=    vBSOC[TP]           # battery state of charge (kWh)\n",
    "        0 <=    vDischarge[TP]      # amount to discharge battery (kW)\n",
    "\n",
    "        0 <=    vPLoss[TP]          # unmet load (kW)\n",
    "\n",
    "        vGrid[TP]                   # positive vGrid means consuming energy from the grid\n",
    "\n",
    "        \n",
    "    end)\n",
    "\n",
    "    #Define another variable to be used for carbon cost calculations (only want vGrid when using grid elec)\n",
    "    @variable(RM, vPosGrid[TP] >= 0)\n",
    "    @constraint(RM, cPosGrid[tp in TP], vPosGrid[tp] >= vGrid[tp])\n",
    "\n",
    "    #Define cost expressions to make code cleaner\n",
    "    #grid operating cost\n",
    "    @expression(RM, grid_op_cost, sum(lmp_df[lmp_df.time .== tp,:lmp][1] * vGrid[tp] * tp_spacing_int for tp in TP))\n",
    "\n",
    "    # hydrogen costs\n",
    "    @expression(RM, elect_cap_cost, calculate_cap_annual(hydrogen_df.electrolyzer_cap_cost_per_kw[1] * vElectroCapkW,ror,\n",
    "                                                            hydrogen_df.electrolyzer_lifetime[1]))\n",
    "    @expression(RM, fc_cap_cost, calculate_cap_annual(hydrogen_df.fuel_cell_cap_cost_per_kw[1] * vFCCapkW, ror,\n",
    "                                                            hydrogen_df.fuelcell_lifetime[1]))\n",
    "    @expression(RM, tank_cap_cost, calculate_cap_annual(hydrogen_df.tank_cap_cost_per_kg[1] * vTankCapkg, ror,\n",
    "                                                            hydrogen_df.tank_lifetime[1]))                                                  \n",
    "    @expression(RM, elect_fom_cost, hydrogen_df.electrolyzer_fom_cost_per_kw[1] * vElectroCapkW)\n",
    "    @expression(RM, fc_fom_cost, hydrogen_df.fuel_cell_fom_cost_per_kw[1] * vFCCapkW)\n",
    "\n",
    "    # battery costs\n",
    "    @expression(RM, batt_cap_cost, calculate_cap_annual(battery_df.cap_cost_per_kw[1] * vBatteryCapkW, ror,\n",
    "                                                            battery_df.lifetime[1]))\n",
    "    @expression(RM, batt_fom_cost, battery_df.fom_cost_per_kw[1] * vBatteryCapkW) \n",
    "    \n",
    "    # EJ cost metrics \n",
    "    @expression(RM, tot_lost_load, sum(vPLoss[tp] * tp_spacing_int for tp in TP))\n",
    "    @expression(RM, reliability_cost, tot_lost_load * voll)   \n",
    "    @expression(RM, diesel_co2, sum(vGEN[\"diesel\",tp] * tp_spacing_int for tp in TP) * \n",
    "                                    gen_df[gen_df.type .== \"diesel\",:carbon_kg_per_kwh][1])\n",
    "    @expression(RM, net_grid_co2, sum(mer_df[mer_df.time .== (tp - Minute(tp)),:mer_kg_kWh][1] * vGrid[tp] * tp_spacing_int\n",
    "                                        for tp in TP))\n",
    "    @expression(RM, tot_grid_co2, sum(mer_df[mer_df.time .== (tp - Minute(tp)),:mer_kg_kWh][1] * vPosGrid[tp] * tp_spacing_int\n",
    "                                        for tp in TP))\n",
    "    @expression(RM, diesel_carb_cost, diesel_co2 * carbon_cost)\n",
    "    @expression(RM, tot_grid_carb_cost, tot_grid_co2 * carbon_cost)    \n",
    "    @expression(RM, tot_carb_cost, diesel_carb_cost + tot_grid_carb_cost)\n",
    "    \n",
    "    # Diesel costs\n",
    "    @expression(RM, diesel_cap_cost, calculate_cap_annual(gen_df[gen_df.type .== \"diesel\", :cap_cost_per_kw][1] * vGenCapkW[\"diesel\"], \n",
    "                            ror,gen_df[gen_df.type .== \"diesel\",:lifetime][1]))\n",
    "    @expression(RM, diesel_fom_cost, gen_df[gen_df.type .== \"diesel\", :fom_per_kw][1] * vGenCapkW[\"diesel\"])\n",
    "    @expression(RM, diesel_vom_cost, sum(gen_df[gen_df.type .== \"diesel\", :om_cost_per_kwh][1] * vGEN[\"diesel\",tp] * \n",
    "                            tp_spacing_int for tp in TP))\n",
    "    @expression(RM, diesel_fuel_cost, sum(gen_df[gen_df.type .== \"diesel\", :fuel_cost_per_kwh][1] * vGEN[\"diesel\",tp] * \n",
    "                            tp_spacing_int for tp in TP))\n",
    "    # Solar costs \n",
    "    @expression(RM, solar_cap_cost, calculate_cap_annual(gen_df[gen_df.type .== \"solar\", :cap_cost_per_kw][1] * vGenCapkW[\"solar\"], \n",
    "                            ror,gen_df[gen_df.type .== \"solar\",:lifetime][1]))\n",
    "    @expression(RM, solar_fom_cost, gen_df[gen_df.type .== \"solar\", :fom_per_kw][1] * vGenCapkW[\"solar\"])\n",
    "    @expression(RM, solar_vom_cost, sum(gen_df[gen_df.type .== \"solar\", :om_cost_per_kwh][1] * vGEN[\"solar\",tp] * \n",
    "                            tp_spacing_int for tp in TP))\n",
    "    @expression(RM, solar_fuel_cost, sum(gen_df[gen_df.type .== \"solar\", :fuel_cost_per_kwh][1] * vGEN[\"solar\",tp] * \n",
    "                            tp_spacing_int for tp in TP))\n",
    "\n",
    "    println(\"here4\")\n",
    "    #Objective Function\n",
    "    @objective(RM, Min, \n",
    "        #diesel costs (capital, fixed o&m, variable o&m, fuel)\n",
    "        diesel_cap_cost + diesel_fom_cost + diesel_vom_cost + diesel_fuel_cost +\n",
    "        #solar costs (capital, fixed o&m, variable o&m, fuel)\n",
    "        solar_cap_cost + solar_fom_cost + solar_vom_cost + solar_fuel_cost +  \n",
    "        #grid costs (fuel) \n",
    "        grid_op_cost + \n",
    "        #hydrogen costs (capital and o&m - no tank o&m)\n",
    "        elect_cap_cost + fc_cap_cost + tank_cap_cost + elect_fom_cost + fc_fom_cost +\n",
    "        #battery costs (capital and o&m)\n",
    "        batt_cap_cost + batt_fom_cost +  \n",
    "        # EJ METRICS\n",
    "        # Reliability Cost (Loss of load) and Carbon Cost - Grid and Diesel \n",
    "        reliability_cost + tot_carb_cost\n",
    "    )\n",
    "    println(\"here5\")\n",
    "    #Constraints\n",
    "    #Supply-Demand Balance Constraints\n",
    "    @constraint(RM, cDemand[tp in TP], \n",
    "        sum(vGEN[g,tp] for g in G) +\n",
    "        vGrid[tp] +\n",
    "        vPLoss[tp] - \n",
    "        vELECTROLYZER[tp] + \n",
    "        vFUELCELL[tp] -\n",
    "        vCharge[tp] +\n",
    "        vDischarge[tp] -  \n",
    "        load_df[load_df.time .== tp,:loadkW][1] == 0\n",
    "    );\n",
    "    println(\"here6\")\n",
    "\n",
    "    # Grid OUTAGE constraint (if during an outage, no net electricity to/from grid)\n",
    "    @constraint(RM, cGridOutage[tp in tp_outage], vGrid[tp] == 0)\n",
    "\n",
    "    # Generator Capacity constraints \n",
    "    @constraint(RM, cCap_dieselBuild, vGenCapkW[\"diesel\"] <= gen_df[gen_df.type .== \"diesel\",:max_capacity_kw][1])\n",
    "    @constraint(RM, cCap_diesel[tp in TP], \n",
    "            vGEN[\"diesel\", tp] <= \n",
    "                    vGenCapkW[\"diesel\"])\n",
    "\n",
    "    # Renewable Generation Capacity Constraints\n",
    "    @constraint(RM, cCap_solarBuild, vGenCapkW[\"solar\"] <= gen_df[gen_df.type .== \"solar\",:max_capacity_kw][1])\n",
    "    @constraint(RM, cCap_solar[tp in TP], \n",
    "            vGEN[\"solar\", tp] <= \n",
    "                    vGenCapkW[\"solar\"] * solarcf_df[solarcf_df.time .== tp,:cf][1])\n",
    "\n",
    "    #RPS Constraint (use instead of carbon cap)\n",
    "    @constraint(RM, cRPS,\n",
    "                    sum(vGEN[\"solar\",tp] * tp_spacing_int for tp in TP) >= \n",
    "                    rps * sum(load_df[load_df.time .== tp,:loadkW][1] * tp_spacing_int for tp in TP))\n",
    "       \n",
    "\n",
    "    \n",
    "    println(\"here7\")\n",
    "    #CARBON CAP (Annual Cap)\n",
    "    @constraint(RM, cCarbCap, \n",
    "                    sum(vGEN[\"diesel\",tp] * tp_spacing_int  * gen_df[gen_df.type .== \"diesel\",:carbon_kg_per_kwh][1] \n",
    "                        for tp in TP) +\n",
    "                    sum(vGrid[tp] * tp_spacing_int * mer_df[mer_df.time .== (tp - Minute(tp)),:mer_kg_kWh][1]\n",
    "                        for tp in TP)\n",
    "                    <= carbon_cap)\n",
    "    \n",
    "    println(\"here8\")\n",
    "    #First Timepoint for storage initialization\n",
    "    tp1 = minimum(load_df.time)\n",
    "    tp_end = maximum(load_df.time)\n",
    "    TP_SOC = setdiff(TP, [tp1])\n",
    "    #HYDROGEN CONSTRAINTS\n",
    "    @constraints(RM, begin\n",
    "    # Electrolyzer, fuel cell, and tank capacity constraints taken care of in build variables\n",
    "    # Electrolyzer, fuel cell, and tank operational constraints \n",
    "        cElectroOpCap[tp in TP], vELECTROLYZER[tp]  <= vElectroCapkW\n",
    "        cFCOpCap[tp in TP], vFUELCELL[tp]  <= vFCCapkW\n",
    "        cTankOpCap[tp in TP], vH2SOC[tp]  <= vTankCapkg\n",
    "\n",
    "    # (1) Hydrogen storage tank state of charge, balance in each timepoint\n",
    "        cH2SOC[tp in TP_SOC], \n",
    "        vH2SOC[tp] == vH2SOC[(tp + Dates.Year(1)) - tp_spacing - Dates.Year(1)] + \n",
    "                        (hydrogen_df.electrolyzer_kg_per_kwh[1])*vELECTROLYZER[tp] * tp_spacing_int - \n",
    "                        vFUELCELL[tp] * tp_spacing_int/(hydrogen_df.fuel_cell_kwh_per_kg[1]) \n",
    "\n",
    "    # (4) Set initial and final storage to half capacity\n",
    "        vH2SOC[tp1] == 0.5 * vTankCapkg + \n",
    "                        (hydrogen_df.electrolyzer_kg_per_kwh[1])*vELECTROLYZER[tp1] * tp_spacing_int - \n",
    "                        vFUELCELL[tp1] * tp_spacing_int/(hydrogen_df.fuel_cell_kwh_per_kg[1]) \n",
    "        vH2SOC[tp_end] == 0.5 * vTankCapkg\n",
    "\n",
    "    end)\n",
    "\n",
    "    #Battery CONSTRAINTS\n",
    "    @constraints(RM, begin\n",
    "    # Battery storage build constraints taken care of in decision variables\n",
    "    # Operational constraints (1) - (3)\n",
    "    cBatteryOpChargeCap[tp in TP], vCharge[tp]  <= vBatteryCapkW \n",
    "    cBatteryOpDischargeCap[tp in TP], vDischarge[tp]  <= vBatteryCapkW \n",
    "    cBatteryOpStorCap[tp in TP], vBSOC[tp]  <= vBatteryCapkW * battery_df.energy_power_ratio[1]\n",
    "    \n",
    "    # (4) Set initial and final storage to half capacity \n",
    "    vBSOC[tp1] == 0.5 * vBatteryCapkW * battery_df.energy_power_ratio[1] + \n",
    "                    battery_df.charge_efficiency[1]*vCharge[tp1]*tp_spacing_int - \n",
    "                    vDischarge[tp1]*tp_spacing_int/(battery_df.discharge_efficiency[1])\n",
    "    vBSOC[tp_end] == 0.5 * vBatteryCapkW * battery_df.energy_power_ratio[1]\n",
    "  \n",
    "    # (5) Battery storage state of charge, balance in each timepoint\n",
    "    cBSOC[tp in TP_SOC], \n",
    "        vBSOC[tp] == vBSOC[(tp + Dates.Year(1)) - tp_spacing - Dates.Year(1)] + \n",
    "                        battery_df.charge_efficiency[1]*vCharge[tp]*tp_spacing_int - \n",
    "                        vDischarge[tp]*tp_spacing_int/(battery_df.discharge_efficiency[1])\n",
    "    \n",
    "    # (6) Limit discharge per year\n",
    "    cBatteryTotDischarge,\n",
    "        sum(vDischarge[tp] for tp in TP) * tp_spacing_int / (battery_df.discharge_efficiency[1]) <=\n",
    "            vBatteryCapkW * battery_df.energy_power_ratio[1] * battery_df.cycles_per_year[1]\n",
    "\n",
    "    end)\n",
    "\n",
    "    #Some final expressions for printing results more easily\n",
    "\n",
    "    #Total Energy Use results\n",
    "    @expression(RM, tot_kwh_solar, sum(vGEN[\"solar\",tp] * tp_spacing_int for tp in TP))\n",
    "    @expression(RM, tot_kwh_diesel, sum(vGEN[\"diesel\",tp] * tp_spacing_int for tp in TP))\n",
    "    @expression(RM, net_kwh_grid, sum(vGrid[tp] * tp_spacing_int for tp in TP)) #net energy to/from grid\n",
    "    @expression(RM, pos_kwh_grid, sum(vPosGrid[tp] * tp_spacing_int for tp in TP)) #total energy taken from grid\n",
    "    @expression(RM, tot_kwh_elect, -1 * sum(vELECTROLYZER[tp] * tp_spacing_int for tp in TP))\n",
    "    @expression(RM, tot_kwh_fc, sum(vFUELCELL[tp] * tp_spacing_int for tp in TP))\n",
    "    @expression(RM, tot_kwh_battcharge, -1 * sum(vCharge[tp] * tp_spacing_int for tp in TP))\n",
    "    @expression(RM, tot_kwh_battdischarge, sum(vDischarge[tp] * tp_spacing_int for tp in TP))\n",
    "\n",
    "    #Total load to cover during outage\n",
    "    @expression(RM, tot_load_outage, sum(load_df[load_df.time .== tp,:loadkW][1] * tp_spacing_int for tp in tp_outage))\n",
    "\n",
    "\n",
    "    # Solve statement (! indicates runs in place)\n",
    "    optimize!(RM)\n",
    "\n",
    "    # RESULTS\n",
    "    # Dataframe of optimal decision variables - solar and diesel generation\n",
    "    gen_solution = create_df(vGEN,\"type\",\"Gen(kW)\");\n",
    "    gen_solution = unstack(gen_solution, :time, :type, :\"Gen(kW)\")\n",
    "    grid_solution = create_df_one_axis(vGrid,\"Grid(kW)\")\n",
    "    #Load loss solution\n",
    "    ploss_solution = create_df_one_axis(vPLoss, \"PLoss(kW)\")\n",
    "    #Load info for reference\n",
    "    load_info = load_df[:,[:time,:loadkW]]\n",
    "    #Hydrogen results\n",
    "    hydrogen_solution = create_df_one_axis(vH2SOC,\"H2SOC(kg)\");\n",
    "    elect_solution = create_df_one_axis(vELECTROLYZER,\"ElectrolyzerRun(kW)\");\n",
    "    fc_solution = create_df_one_axis(vFUELCELL,\"FuelCellRun(kW)\");\n",
    "    #Battery results\n",
    "    battery_solution = create_df_one_axis(vBSOC,\"BSOC(kWh)\");\n",
    "    bcharge_solution = create_df_one_axis(vCharge,\"ChargeBattery(kW)\");\n",
    "    bdischarge_solution = create_df_one_axis(vDischarge,\"DischargeBattery(kW)\");\n",
    "\n",
    "    #Combine all variables into one dataframe\n",
    "    gen_solution = innerjoin(gen_solution, grid_solution,elect_solution, hydrogen_solution, fc_solution, bcharge_solution, battery_solution, \n",
    "                                bdischarge_solution,ploss_solution,load_info, on = :time)\n",
    "\n",
    "    #Total Load (kWh)\n",
    "    total_load = sum(load_df[load_df.time .== tp,:loadkW][1] * tp_spacing_int for tp in TP);\n",
    "\n",
    "    # RESULTS FOR REPORT USE HERE\n",
    "    report_results = DataFrame(Category = String[], Metric = String[], Value = Float32[], Unit = String[])\n",
    "\n",
    "    #Add objective costs\n",
    "    push!(report_results, [\"Cost\"  \"Total Cost\" objective_value(RM) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Solar Capital Cost\" value(solar_cap_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Diesel Capital Cost\" value(diesel_cap_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Electrolyzer Capital Cost\" value(elect_cap_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Fuel Cell Capital Cost\" value(fc_cap_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Tank Capital Cost\" value(tank_cap_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Battery Capital Cost\" value(batt_cap_cost) \"\\$\"])\n",
    "\n",
    "    investment_cost = value(solar_cap_cost) + value(diesel_cap_cost) + value(elect_cap_cost) + \n",
    "                        value(fc_cap_cost) + value(tank_cap_cost) + value(batt_cap_cost)\n",
    "    push!(report_results, [\"Cost\" \"Total Annualized Investment Cost\" investment_cost \"\\$\"])\n",
    "\n",
    "    push!(report_results, [\"Cost\"  \"Solar FOM Cost\" value(solar_fom_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Diesel FOM Cost\" value(diesel_fom_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Diesel Fuel Cost\" value(diesel_fuel_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Grid Electricity Cost\" value(grid_op_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Electrolyzer FOM Cost\" value(elect_fom_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Fuel Cell FOM Cost\" value(fc_fom_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Battery FOM Cost\" value(batt_fom_cost) \"\\$\"]) \n",
    "\n",
    "    operation_cost = value(solar_fom_cost) + value(diesel_fom_cost) + value(diesel_fuel_cost) + \n",
    "                        value(grid_op_cost) + value(elect_fom_cost) + value(fc_fom_cost) + \n",
    "                        value(batt_fom_cost)\n",
    "    push!(report_results, [\"Cost\" \"Total Annual Operation Cost\" operation_cost \"\\$\"])\n",
    "\n",
    "    push!(report_results, [\"Cost\"  \"Lost Load Cost\" value(reliability_cost) \"\\$\"])\n",
    "    push!(report_results, [\"Cost\"  \"Carbon Cost\" value(tot_carb_cost) \"\\$\"])\n",
    "\n",
    "    ej_cost = value(reliability_cost) + value(tot_carb_cost)\n",
    "    push!(report_results, [\"Cost\"  \"EJ Cost\" ej_cost \"\\$\"])\n",
    "\n",
    "    #Add EJ Metrics\n",
    "    push!(report_results, [\"EJ\"  \"Total Lost Load\" value(tot_lost_load) \"kWh\"])\n",
    "    push!(report_results, [\"EJ\"  \"Diesel Emissions\" value(diesel_co2) \"kg CO2\"])\n",
    "    push!(report_results, [\"EJ\"  \"Net Grid Emissions\" value(net_grid_co2) \"kg CO2\"])\n",
    "    push!(report_results, [\"EJ\"  \"Total Grid Emissions\" value(tot_grid_co2) \"kg CO2\"])\n",
    "    tot_co2 = value(diesel_co2) + value(tot_grid_co2)\n",
    "    net_co2 = value(diesel_co2) + value(net_grid_co2)\n",
    "    push!(report_results, [\"EJ\"  \"Total Emissions\" tot_co2 \"kg CO2\"])\n",
    "    push!(report_results, [\"EJ\"  \"Net Emissions\" net_co2 \"kg CO2\"])\n",
    "\n",
    "    #Add Build Capacities\n",
    "    push!(report_results, [\"Capacity\"  \"Solar\" value(vGenCapkW[\"solar\"]) \"kW\"]) \n",
    "    push!(report_results, [\"Capacity\"  \"Diesel\" value(vGenCapkW[\"diesel\"]) \"kW\"]) \n",
    "    push!(report_results, [\"Capacity\"  \"Electrolyzer\" value(vElectroCapkW) \"kW\"]) \n",
    "    push!(report_results, [\"Capacity\"  \"Fuel Cell\" value(vFCCapkW) \"kW\"]) \n",
    "    push!(report_results, [\"Capacity\"  \"Tank\" value(vTankCapkg) \"kg H2\"]) \n",
    "    push!(report_results, [\"Capacity\"  \"Battery (4h)\" value(vBatteryCapkW) \"kW\"])\n",
    "\n",
    "    #Add Energy Use\n",
    "    push!(report_results, [\"Energy Output\"  \"Solar\" value(tot_kwh_solar) \"kWh\"])\n",
    "    push!(report_results, [\"Energy Output\"  \"Diesel\" value(tot_kwh_diesel) \"kWh\"])\n",
    "    push!(report_results, [\"Energy Output\"  \"Net Grid\" value(net_kwh_grid) \"kWh\"])\n",
    "    push!(report_results, [\"Energy Output\"  \"Positive Grid\" value(pos_kwh_grid) \"kWh\"])\n",
    "    push!(report_results, [\"Energy Output\"  \"Fuel Cell\" value(tot_kwh_fc) \"kWh\"])\n",
    "    push!(report_results, [\"Energy Output\"  \"Electrolyzer\" value(tot_kwh_elect) \"kWh\"])\n",
    "    push!(report_results, [\"Energy Output\"  \"Battery Discharge\" value(tot_kwh_battdischarge) \"kWh\"])\n",
    "    push!(report_results, [\"Energy Output\"  \"Battery Charge\" value(tot_kwh_battcharge) \"kWh\"])\n",
    "    push!(report_results, [\"Energy Output\"  \"Annual Load\" value(total_load) \"kWh\"])\n",
    "    push!(report_results, [\"Energy Output\"  \"Total Load during Outage\" value(tot_load_outage) \"kWh\"])\n",
    "\n",
    "\n",
    "    #Add Relevant Inputs\n",
    "    push!(report_results, [\"Input\"  \"Carbon Cost\" carbon_cost \"\\$/kg CO2\"])\n",
    "    push!(report_results, [\"Input\"  \"VoLL\" voll \"\\$/kWh\"])\n",
    "    push!(report_results, [\"Input\"  \"Hydrogen Electrolyzer Cost\" hydrogen_df.electrolyzer_cap_cost_per_kw[1] \"\\$/kW\"])\n",
    "    push!(report_results, [\"Input\"  \"Outage Length\" outage_length \"Days\"])\n",
    "    push!(report_results, [\"Input\"  outage_start 0.0 \"Outage Start Date\"])\n",
    "    push!(report_results, [\"Input\"  \"RPS\" rps \"None\"])\n",
    "    # END OF RESULTS FOR REPORT USE\n",
    "\n",
    "    # Return the solution and objective as named tuple\n",
    "    return (\n",
    "        gen_solution = gen_solution, #includes all timestamped variables\n",
    "        #cost = objective_value(RM),\n",
    "        status = termination_status(RM),\n",
    "        report_results = report_results\n",
    "    )\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 3. Loop through Runs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "config_SanJoaquin_carb25.csv\n",
      "config_SanJoaquin_carb50.csv\n",
      "config_SanJoaquin_carb100.csv\n",
      "config_SanJoaquin_carb200.csv\n"
     ]
    }
   ],
   "source": [
    "for row in eachrow(runs_df)\n",
    "    run = row.runs \n",
    "    #Load Config File\n",
    "    config_filename = string(\"config_\", county, \"_\", run, \".csv\")\n",
    "    datadir = joinpath(inputs_dir,\"ConfigFiles\",county,config_filename)\n",
    "    config = CSV.read(datadir, DataFrame);\n",
    "\n",
    "\n",
    "    #Load files from Config inputs\n",
    "    gen_filename = config[config[!,:Parameter] .== \"generator_build_file\", :Value][1]\n",
    "    solar_cf_filename = config[config[!,:Parameter] .== \"solar_cf_file\", :Value][1]\n",
    "    lmp_filename = config[config[!,:Parameter] .== \"lmp_file\", :Value][1]\n",
    "    grid_mer_filename = config[config[!,:Parameter] .== \"grid_mer_file\", :Value][1]\n",
    "    load_filename = config[config[!,:Parameter] .== \"load_file\", :Value][1]\n",
    "    load_setting = config[config[!,:Parameter] .== \"load_setting\", :Value][1]\n",
    "    hydrogen_filename = config[config[!,:Parameter] .== \"hydrogen_file\", :Value][1]\n",
    "    battery_filename = config[config[!,:Parameter] .== \"battery_file\", :Value][1]\n",
    "    load_setting = config[config[!,:Parameter] .== \"load_setting\", :Value][1]\n",
    "\n",
    "    #Load Generator Build Inputs\n",
    "    datadir = joinpath(inputs_dir,\"BuildInputs\",gen_filename); \n",
    "    gen_df = CSV.read(datadir, DataFrame);\n",
    "\n",
    "    #Load Solar CF Inputs\n",
    "    datadir = joinpath(inputs_dir,\"SolarProfiles\",solar_cf_filename); \n",
    "    solarcf_df = CSV.read(datadir, DataFrame);\n",
    "    #Convert solar cf time column to datetime format\n",
    "    solarcf_df.time = DateTime.(solarcf_df.time,dateformat\"y-m-d HH:MM:S+zz\");\n",
    "\n",
    "    #Load LMP Inputs\n",
    "    datadir = joinpath(inputs_dir,\"LMPs\",lmp_filename);  \n",
    "    lmp_df = CSV.read(datadir, DataFrame);\n",
    "    #Convert time column to datetime format\n",
    "    lmp_df.time = String.(lmp_df.time)\n",
    "    tzdf = Dates.DateFormat(\"yyyy-mm-ddTH:M:S-zz\")\n",
    "    lmp_df.time = DateTime.(ZonedDateTime.(lmp_df.time,tzdf));\n",
    "    #Convert lmp column to $/kWh from $/MWh\n",
    "    lmp_df.lmp = lmp_df.lmp./1000;\n",
    "    #Remove feb 29 (leap year) from lmp data\n",
    "    lmp_df = filter(:time => x -> Date(x) != Dates.Date(\"2020-02-29\"), lmp_df);\n",
    "\n",
    "    #Load MER inputs\n",
    "    datadir = joinpath(inputs_dir,\"BuildInputs\",grid_mer_filename);  \n",
    "    mer_df = CSV.read(datadir, DataFrame);\n",
    "    #Convert time column to datetime format\n",
    "    mer_df.time = String.(mer_df.time)\n",
    "    tzdf = Dates.DateFormat(\"yyyy-mm-dd H:M:S+zz\")\n",
    "    mer_df.time = DateTime.(ZonedDateTime.(mer_df.time,tzdf));\n",
    "    #Remove feb 29 (leap year) from mer data\n",
    "    mer_df = filter(:time => x -> Date(x) != Dates.Date(\"2020-02-29\"), mer_df);\n",
    "\n",
    "    #Load load profiles\n",
    "    datadir = joinpath(inputs_dir,\"LoadProfiles\",load_setting,load_filename);\n",
    "    load_df = CSV.read(datadir, DataFrame);\n",
    "    #Convert load time column to datetime format\n",
    "    load_df.time = DateTime.(load_df.time,dateformat\"yyyy-mm-dd HH:MM:SS+zz\");\n",
    "\n",
    "    #Check timestamps align\n",
    "    println(sort(lmp_df.time) == solarcf_df.time)\n",
    "    println(solarcf_df.time == load_df.time)\n",
    "\n",
    "    #Load hydrogen inputs\n",
    "    datadir = joinpath(inputs_dir,\"BuildInputs\",hydrogen_filename);  \n",
    "    hydrogen_df = CSV.read(datadir, DataFrame);\n",
    "\n",
    "    #Load battery inputs\n",
    "    datadir = joinpath(inputs_dir,\"BuildInputs\",battery_filename);  \n",
    "    battery_df = CSV.read(datadir,DataFrame);\n",
    "\n",
    "    #RUN SOLVER\n",
    "    solution = renewable_microgrid(config, gen_df, solarcf_df, load_df, hydrogen_df, lmp_df, mer_df, battery_df);\n",
    "\n",
    "    #PRINT OUTPUTS\n",
    "    gen_solution = solution[:gen_solution];\n",
    "    report_solution = solution[:report_results];\n",
    "    datadir = joinpath(results_dir,county,string(run, \"_genresults.csv\"));\n",
    "    CSV.write(datadir,gen_solution);\n",
    "    datadir = joinpath(results_dir,county,string(run,\"_reportresults.csv\"));\n",
    "    CSV.write(datadir,report_solution);\n",
    "\n",
    "    println(string(county, run, \"_outputs completed\"))\n",
    "\n",
    "\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Load Solar Capacity Factors"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.0",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.0"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
